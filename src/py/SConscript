# PARTIO SOFTWARE
# Copyright 2010 Disney Enterprises, Inc. All rights reserved
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
# 
# * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
# 
# * Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in
# the documentation and/or other materials provided with the
# distribution.
# 
# * The names "Disney", "Walt Disney Pictures", "Walt Disney Animation
# Studios" or the names of its contributors may NOT be used to
# endorse or promote products derived from this software without
# specific prior written permission from Walt Disney Pictures.
# 
# Disclaimer: THIS SOFTWARE IS PROVIDED BY WALT DISNEY PICTURES AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING,
# BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE, NONINFRINGEMENT AND TITLE ARE DISCLAIMED.
# IN NO EVENT SHALL WALT DISNEY PICTURES, THE COPYRIGHT HOLDER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
# EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
# PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
# PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND BASED ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
import os
import sys
from distutils import sysconfig

python_ver = sys.version[:3]
python_inc = 'include/python%s' % python_ver
python_mod = 'lib/python%s/site-packages' % python_ver

Import('env variant_build_abs variant_install_abs zlib_dir zlib_name swig_dir')

envpy = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                  LIBPATH="%s/lib" % variant_build_abs,
                  LIBS=["partio", zlib_name])

if zlib_dir:
    envpy.Append(CPPPATH=[zlib_dir + "/include"])
    envpy.Append(LIBPATH=[zlib_dir + "/lib/x64"])
elif sys.platform == "win32":
    print("WARNING: You may want to specify zlib directory using with-zlib=<path> (zlib package can be found in winReqLibs folder)")

if swig_dir:
    envpy["SWIG"] = swig_dir + "/swig"
elif sys.platform == "win32" and envpy.WhereIs("swig") is None:
    print("WARNING: You may want to specify swig directory using with-swig=<path> (swig package can be found in winReqLibs folder)")

swig_builder = Builder(action='$SWIG -o $TARGET -c++ -python -Wall $SOURCE', suffix='.cc', src_suffix='.i')
envpy.Append(BUILDERS={"SwigGen": swig_builder})

envpy.Append(CPPPATH=[sysconfig.get_python_inc()])
envpy["SHLIBPREFIX"] = ""
envpy["SHLIBSUFFIX"] = sysconfig.get_config_var("SO")

if sys.platform == "win32":
    envpy.Append(LIBPATH=[sysconfig.PREFIX+'/libs'])
    envpy.Append(LIBS="python%s" % python_ver.replace(".", ""))
else:
    envpy.Append(CXXFLAGS=["-Wno-format"])
    if sys.platform == "darwin":
        envpy.Append(LINKFLAGS=["-framework", "python"])
    else:
        envpy.Append(LIBS="python%s" % python_ver)

src = envpy.SwigGen("partio_wrap.cc", "partio.i")
lib = envpy.SharedLibrary("_partio", [src])
dp1 = envpy.Install(variant_install_abs + "/" + python_mod, lib)
dp2 = envpy.Install(variant_install_abs + "/" + python_mod, "partio.py")
envpy.Depends(dp1, dp2)

envpy.Alias('python', [dp1, dp2])
