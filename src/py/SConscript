# PARTIO SOFTWARE
# Copyright 2010 Disney Enterprises, Inc. All rights reserved
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
# 
# * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
# 
# * Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in
# the documentation and/or other materials provided with the
# distribution.
# 
# * The names "Disney", "Walt Disney Pictures", "Walt Disney Animation
# Studios" or the names of its contributors may NOT be used to
# endorse or promote products derived from this software without
# specific prior written permission from Walt Disney Pictures.
# 
# Disclaimer: THIS SOFTWARE IS PROVIDED BY WALT DISNEY PICTURES AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING,
# BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE, NONINFRINGEMENT AND TITLE ARE DISCLAIMED.
# IN NO EVENT SHALL WALT DISNEY PICTURES, THE COPYRIGHT HOLDER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
# EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
# PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
# PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND BASED ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
import os
import sys
from distutils import sysconfig

python_ver = sys.version[:3]
python_inc = 'include/python%s' % python_ver
python_mod = 'lib/python%s/site-packages' % python_ver

Import('env variant_build_abs variant_install_abs')

envpy = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                  LIBPATH="%s/lib" % variant_build_abs,
                  LIBS=["partio", env["ZLIB_LIB_NAME"]])

zlib_root = envpy["ZLIB_ROOT"]

zlib_inc = envpy["ZLIB_INC_DIR"]
if not zlib_inc and zlib_root:
    zlib_inc += zlib_root + "/include"

if zlib_inc:
    envpy.Append(CPPPATH=[zlib_inc])

zlib_lib = envpy["ZLIB_LIB_DIR"]
if not zlib_lib and zlib_root:
    zlib_lib += zlib_root + "/lib"

if zlib_lib:
    envpy.Append(LIBPATH=[zlib_lib])

if not envpy["SWIG"]:
    envpy["SWIG"] = envpy.WhereIs("swig")

swig_builder = Builder(action='$SWIG -o $TARGET -c++ -python -Wall $SOURCE', suffix='.cc', src_suffix='.i')
envpy.Append(BUILDERS={"SwigGen": swig_builder})

envpy.Append(CPPPATH=[sysconfig.get_python_inc()])
envpy["SHLIBPREFIX"] = ""
envpy["SHLIBSUFFIX"] = sysconfig.get_config_var("SO")

if sys.platform == "win32":
    envpy.Append(LIBPATH=[sysconfig.PREFIX+'/libs'])
    envpy.Append(LIBS="python%s" % python_ver.replace(".", ""))
else:
    envpy.Append(CXXFLAGS=["-Wno-format"])
    if sys.platform == "darwin":
        # Change from '$LINKFLAGS -dynamiclib'
        envpy['SHLINKFLAGS'] = '$LINKFLAGS -bundle'
        envpy.Append(LINKFLAGS=" -undefined dynamic_lookup")
    else:
        envpy.Append(LIBS="python%s" % python_ver)

src = envpy.SwigGen("partio_wrap.cc", "partio.i")
lib = envpy.SharedLibrary("_partio", [src])
dp1 = envpy.Install(variant_install_abs + "/" + python_mod, lib)
dp2 = envpy.Install(variant_install_abs + "/" + python_mod, "partio.py")
envpy.Depends(dp1, dp2)

envpy.Alias('python', [dp1, dp2])
