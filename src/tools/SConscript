# PARTIO SOFTWARE
# Copyright 2010 Disney Enterprises, Inc. All rights reserved
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
# 
# * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
# 
# * Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in
# the documentation and/or other materials provided with the
# distribution.
# 
# * The names "Disney", "Walt Disney Pictures", "Walt Disney Animation
# Studios" or the names of its contributors may NOT be used to
# endorse or promote products derived from this software without
# specific prior written permission from Walt Disney Pictures.
# 
# Disclaimer: THIS SOFTWARE IS PROVIDED BY WALT DISNEY PICTURES AND
# CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING,
# BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS
# FOR A PARTICULAR PURPOSE, NONINFRINGEMENT AND TITLE ARE DISCLAIMED.
# IN NO EVENT SHALL WALT DISNEY PICTURES, THE COPYRIGHT HOLDER OR
# CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
# EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
# PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
# PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND BASED ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGES.
import os
import sys

Import('env variant_build_abs variant_install_abs zlib_dir zlib_name glut_dir')

envtools = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                     LIBPATH="%s/lib" % variant_build_abs,
                     LIBS=["partio", zlib_name])

if zlib_dir:
    envtools.Append(CPPPATH=[zlib_dir + "/include"])
    envtools.Append(LIBPATH=[zlib_dir + "/lib/x64"])
elif sys.platform == "win32":
    print("WARNING: You may want to specify zlib directory using with-zlib=<path> (zlib package can be found in winReqLibs folder)")

# Find all .cpp's and make them into programs with same names as cpp
ignores = set(["partview.cpp", "partconvert.cpp"])
thisdir = Dir(".").srcnode().abspath
srcs = [x for x in os.listdir(thisdir) if x.endswith(".cpp") and x not in ignores]
targets = [x.replace(".cpp", "") for x in srcs]
tools = []

for i in range(len(srcs)):
    src = srcs[i]
    target = targets[i]
    prog = envtools.Program(target, [src])
    envtools.Install("../../bin", prog)
    tools.append(envtools.Install(variant_install_abs + "/bin", prog))

envglut = envtools.Clone()
if glut_dir:
    envglut.Append(CPPPATH=[glut_dir + "/include"])
    envglut.Append(LIBPATH=[glut_dir + "/lib/x64"])
elif sys.platform == "win32":
    print("WARNING: You may want to specify glut directory using with-glut=<path> (glut package can be found in winReqLibs folder)")

if envglut["mac"]:
    frameworks=["-framework","OpenGL","-framework","GLUT"]
    envglut.Append(LINKFLAGS=frameworks)
else:
    if sys.platform == "win32":
        envglut.Append(LIBS=["opengl32", "glu32", "freeglut"])
    else:
        envglut.Append(LIBS=["glut","GL","GLU"])

prog = envglut.Program("partview", ["partview.cpp"])
envglut.Install("../../bin", prog)
tools.append(envglut.Install(variant_install_abs + "/bin", prog))

if sys.platform == "win32":
    if glut_dir:
        tools.append(envglut.Install(variant_install_abs + "/bin", glut_dir + "/bin/x64/freeglut.dll"))

env.Alias('tools', tools)
