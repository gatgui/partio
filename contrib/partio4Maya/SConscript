import os
import sys

Import('env variant_build_abs variant_install_abs zlib_name zlib_dir GetInstallPath')

mayaVer = ARGUMENTS.get("maya-ver", "2013")
mayaDir = ARGUMENTS.get("maya-dir", None)
mayaInc = ARGUMENTS.get("maya-inc-dir", None)
mayaLib = ARGUMENTS.get("maya-lib-dir", None)
if mayaDir is None:
    if sys.platform == "win32":
        mayaDir = "C:/Program Files/Autodesk/Maya%s" % mayaVer
        if mayaInc is None:
            mayaInc = mayaDir + "/include"
        if mayaLib is None:
            mayaLib = mayaDir + "/lib"
    elif sys.platform == "darwin":
        # TODO!
        mayaDir = "/Applications/Autodesk/maya%s" % mayaVer
        if mayaInc is None:
            mayaInc = mayaDir + "/devkit/include"
        if mayaLib is None:
            mayaLib = mayaDir + "/Maya.app/Contents/MacOS"
    else:
        mayaDir = "/usr/autodesk/maya%s-x64" % mayaVer
        if mayaInc is None:
            mayaInc = mayaDir + "/include"
        if mayaLib is None:
            mayaLib = mayaDir + "/lib"

mayaenv = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                    LIBPATH=[variant_build_abs + "/lib"],
                    LIBS=["partio", zlib_name])

if zlib_dir:
    mayaenv.Append(CPPPATH=[zlib_dir + "/include"])
    mayaenv.Append(LIBPATH=[zlib_dir + "/lib/x64"])
elif sys.platform == "win32":
    print("WARNING: You may want to specify zlib directory using with-zlib=<path> (zlib package can be found in winReqLibs folder)")

mayaenv.Append(CPPDEFINES=["_BOOL", "REQUIRE_IOSTREAM"])
mayaenv.Append(CPPPATH=[mayaInc])
mayaenv.Append(LIBPATH=[mayaLib])
mayaenv.Append(LIBS=["OpenMaya", "Foundation", "OpenMayaAnim", "OpenMayaFX", "OpenMayaUI", "OpenMayaRender"])
if sys.platform == "win32":
    # use regex
    mayaenv.Append(CPPDEFINES=["NT_PLUGIN", "REGEX_STATIC"])
    mayaenv.Append(CPPPATH=["../../gto/regex/src"])
    mayaenv.Append(LIBS=["opengl32", "glu32", "shell32"])
    pluginExt = ".mll"
else:
    if sys.platform == "darwin":
        mayaenv.Append(CPPDEFINES=["MAC_PLUGIN", "OSMac_", "OSMac_MachO"])
        mayaenv.Append(LINKFLAGS=" -framework OpenGL")
        pluginExt = ".bundle"
    else:
        mayaenv.Append(CPPDEFINES=["LINUX"])
        mayaenv.Append(LIBS=["GL", "GLU"])
        pluginExt = ".so"
mayaenv["SHLIBPREFIX"] = ""
mayaenv["SHLIBSUFFIX"] = pluginExt

thisdir = Dir(".").srcnode().abspath

def cppFiles(basePath):
    return [x for x in os.listdir(basePath) if x.endswith(".cpp")]

srcs = cppFiles(thisdir)
plugin = mayaenv.LoadableModule("partio4Maya"+pluginExt, srcs)

build_dir = variant_build_abs + "/maya/%s" % mayaVer
out_dir = variant_install_abs + "/maya/%s" % mayaVer

mayaenv.Install(build_dir + "/icons", Glob(thisdir + "/icons/*.png"))
mayaenv.Install(build_dir + "/scripts", Glob(thisdir + "/scripts/*.mel"))
mayaenv.Install(build_dir + "/plug-ins", plugin)

maya_targets = [mayaenv.Install(out_dir + "/icons", Glob(thisdir + "/icons/*.png")),
                mayaenv.Install(out_dir + "/scripts", Glob(thisdir + "/scripts/*.mel")),
                mayaenv.Install(out_dir + "/scripts", Glob(thisdir + "/scripts/*.py")),
                mayaenv.Install(out_dir + "/plug-ins", plugin)]

env.Alias('maya', maya_targets)
