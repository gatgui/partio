import os
import sys

Import('env variant_build_abs variant_install_abs GetInstallPath')

mayaenv = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                    LIBPATH=[variant_build_abs + "/lib"],
                    LIBS=["partio", env["ZLIB_LIB_NAME"]])

zlib_root = mayaenv["ZLIB_ROOT"]

zlib_inc = mayaenv["ZLIB_INC_DIR"]
if not zlib_inc and zlib_root:
    zlib_inc += zlib_root + "/include"

if zlib_inc:
    mayaenv.Append(CPPPATH=[zlib_inc])

zlib_lib = mayaenv["ZLIB_LIB_DIR"]
if not zlib_lib and zlib_root:
    zlib_lib += zlib_root + "/lib"

if zlib_lib:
    mayaenv.Append(LIBPATH=[zlib_lib])

maya_root = mayaenv["MAYA_ROOT"]

if not maya_root:
    if sys.platform == "win32":
        maya_root = "C:/Program Files/Autodesk/Maya%s" % mayaenv["MAYA_VERSION"]
    elif sys.platform == "darwin":
        maya_root = "/Applications/Autodesk/maya%s" % mayaenv["MAYA_VERSION"]
    else:
        if float(mayaenv["MAYA_VERSION"]) < 2016:
            maya_root = "/usr/autodesk/maya%s-x64" % mayaenv["MAYA_VERSION"]
        else:
            maya_root = "/usr/autodesk/maya%s" % mayaenv["MAYA_VERSION"]
    if not os.path.isdir(maya_root):
        print("Invalid Maya root directory '%s'. Use MAYA_VERSION= or MAYA_ROOT= flags." % maya_root)
        maya_root = ""

maya_inc = mayaenv["MAYA_INC_DIR"]

if not maya_inc and maya_root:
    if sys.platform == "darwin":
        maya_inc = maya_root + "/devkit/include"
    else:
        maya_inc = maya_root + "/include"

if maya_inc:
    mayaenv.Append(CPPPATH=[maya_inc])

maya_lib = mayaenv["MAYA_LIB_DIR"]

if not maya_lib and maya_root:
    if sys.platform == "darwin":
        maya_lib = maya_root + "/Maya.app/Contents/MacOS"
    else:
        maya_lib = maya_root + "/lib"

if maya_lib:
    mayaenv.Append(LIBPATH=[maya_lib])

mayaenv.Append(CPPDEFINES=["_BOOL", "REQUIRE_IOSTREAM"])
mayaenv.Append(LIBS=["OpenMaya", "Foundation", "OpenMayaAnim", "OpenMayaFX", "OpenMayaUI", "OpenMayaRender"])
if sys.platform == "win32":
    # use regex
    mayaenv.Append(CPPDEFINES=["NT_PLUGIN", "REGEX_STATIC"])
    mayaenv.Append(CPPPATH=["../../gto/regex/src"])
    mayaenv.Append(LIBS=["opengl32", "glu32", "shell32"])
    pluginExt = ".mll"
else:
    if sys.platform == "darwin":
        mayaenv.Append(CPPDEFINES=["MAC_PLUGIN", "OSMac_", "OSMac_MachO"])
        mayaenv.Append(LINKFLAGS=" -framework OpenGL")
        pluginExt = ".bundle"
    else:
        mayaenv.Append(CPPDEFINES=["LINUX"])
        mayaenv.Append(LIBS=["GL", "GLU"])
        pluginExt = ".so"
mayaenv["SHLIBPREFIX"] = ""
mayaenv["SHLIBSUFFIX"] = pluginExt

thisdir = Dir(".").srcnode().abspath

def cppFiles(basePath):
    return [x for x in os.listdir(basePath) if x.endswith(".cpp")]

srcs = cppFiles(thisdir)
plugin = mayaenv.LoadableModule("partio4Maya"+pluginExt, srcs)

build_dir = variant_build_abs + "/maya/%s" % mayaenv["MAYA_VERSION"]
out_dir = variant_install_abs + "/maya/%s" % mayaenv["MAYA_VERSION"]

mayaenv.Install(build_dir + "/icons", Glob(thisdir + "/icons/*.png"))
mayaenv.Install(build_dir + "/scripts", Glob(thisdir + "/scripts/*.mel"))
mayaenv.Install(build_dir + "/plug-ins", plugin)

maya_targets = [mayaenv.Install(out_dir + "/icons", Glob(thisdir + "/icons/*.png")),
                mayaenv.Install(out_dir + "/scripts", Glob(thisdir + "/scripts/*.mel")),
                mayaenv.Install(out_dir + "/scripts", Glob(thisdir + "/scripts/*.py")),
                mayaenv.Install(out_dir + "/plug-ins", plugin)]

env.Alias('maya', maya_targets)
