import os
import sys

Import('env variant_build_abs variant_install_abs GetInstallPath')

mayaVer = ARGUMENTS.get("maya-ver", "2013")
mayaDir = ARGUMENTS.get("maya-dir", None)
if mayaDir is None:
    if sys.platform == "win32":
        mayaDir = "C:/Program Files/Autodesk/Maya%s" % mayaVer
    elif sys.platform == "darwin":
        # TODO!
        mayaDir = "/Applications/Autodesk/Maya%s.app" % mayaVer
    else:
        mayaDir = "/usr/autodesk/maya%s-x64" % mayaVer

mayaenv = env.Clone(CPPPATH=[variant_build_abs + "/include"],
                    LIBPATH=[variant_build_abs + "/lib"],
                    LIBS=["partio", "z"])

mayaenv.Append(CPPDEFINES=["_BOOL", "REQUIRE_IOSTREAM"])
mayaenv.Append(CPPPATH=[os.path.join(mayaDir, "include")])
mayaenv.Append(LIBPATH=[os.path.join(mayaDir, "lib")])
mayaenv.Append(LIBS=["OpenMaya", "Foundation", "OpenMayaAnim", "OpenMayaFX", "OpenMayaUI", "OpenMayaRender", "z"])
if sys.platform == "win32":
    mayaenv.Append(LIBS=["opengl32", "glu32"])
    pluginExt = ".mll"
else:
    if sys.platform == "darwin":
        mayaenv.Append(CPPDEFINES=["MAC_PLUGIN", "OSMac_", "OSMac_MachO"])
        mayaenv.Append(LINKFLAGS=" -framework OpenGL")
        pluginExt = ".bundle"
    else:
        mayaenv.Append(CPPDEFINES=["LINUX"])
        mayaenv.Append(LIBS=["GL", "GLU"])
        pluginExt = ".so"

thisdir = Dir(".").srcnode().abspath

def cppFiles(basePath):
    return [x for x in os.listdir(basePath) if x.endswith(".cpp")]

srcs = cppFiles(thisdir)
print("srcs = %s" % srcs)
plugin = mayaenv.LoadableModule("partio4Maya" + pluginExt, srcs)

build_dir = variant_build_abs + "/maya/%s" % mayaVer
out_dir = variant_install_abs + "/maya/%s" % mayaVer

mayaenv.Install(build_dir + "/icons", Glob(thisdir + "/icons/*.png"))
mayaenv.Install(build_dir + "/scripts", Glob(thisdir + "/scripts/*.mel"))
mayaenv.Install(build_dir + "/plug-ins", plugin)

maya_targets = [mayaenv.Install(out_dir + "/icons", Glob(thisdir + "/icons/*.png")),
                mayaenv.Install(out_dir + "/scripts", Glob(thisdir + "/scripts/*.mel")),
                mayaenv.Install(out_dir + "/plug-ins", plugin)]

env.Alias('maya', maya_targets)
